#!/usr/bin/perl -w

# Maildir flags are:
#         D (draft)
#         F (flagged)
#         R (replied)
#         S (seen)
#         T (trashed)
# and must occur in ASCII order.
#
# flagmatchre = re.compile(':.*2,([A-Z]+)')
#
# filename:2,F   => .nnmaildir/marks/tick/filename
# filename:2,R   => .nnmaildir/marks/reply/filename
# filename:2,S   => .nnmaildir/marks/read/filename

use strict;
use File::Basename;
use Getopt::Long;
$Getopt::Long::ignorecase = 0;

my $sep_char = ':';
if (qx(uname) =~ m/^cygwin/i) {
    $sep_char = '!';
}

my $from_gnus = 0;
my $from_maildir = 0;
my $dir = "~/Maildir";
GetOptions('-g' => \$from_gnus,
           '-m' => \$from_maildir,
           '-d=s' => \$dir);

if (! ($from_gnus ^ $from_maildir)) {
  die "Usage: sync_nnmaildir -g (from gnus) [-f]\n   or: sync_nnmaildir -m (from maildir) [-v -f]\n";
}

sub debug(@) {
    print STDERR "@_\n";
}

for (glob "$dir/*") {
  my $mb = $_;
  mkdir "$mb/.nnmaildir";
  mkdir "$mb/.nnmaildir/marks";

  for (glob "$mb/cur/*") {
    my $file = $_;

    if (not m!(.*?)/cur/(.*?)(?:$sep_char)\b2,(.*)$!) {
        warn "$_ not match maildir file\n";
        next;
    }

    my $path = $1;
    my $message = $2;
    my $flags = $3;

    if ($message =~ m/:/) {
        system("bhj-notify sync_nnmaildir interesting double separator");
        die "interesting: $_\n";
    }

    if ($from_maildir) {

        my %flag_settings = (tick => qr/F/,
                             read => qr/S/,
                             reply => qr/R/);

        for my $folder (sort keys %flag_settings) {
            my $re = $flag_settings{$folder};
            my $dst = "$path/.nnmaildir/marks/$folder/$message";
            if ($flags =~ $re) {
                mkdir "$path/.nnmaildir/marks/$folder";
                if (! -e $dst) {
                    unlink $dst if -l $dst;
                    symlink "$file","$dst"
                        and print "Sync: added mail in $mb to gnus $folder\n"
                        or warn "symlink $file $dst failed\n";
                }
            } else {
                unlink "$dst"
                    and print "Sync: removed mail in $mb from nnmaildir $folder\n";
            }

        }
    } elsif ($from_gnus) {
      my $new_flags = '';

      my %flag_settings = (tick => q/F/,
                           read => q/S/,
                           reply => q/R/);

      for my $folder (sort keys %flag_settings) {
            my $re = $flag_settings{$folder};
            my $dst = "$path/.nnmaildir/marks/$folder/$message";

            if (-e $dst or -l $dst or $flags =~ m/$re/) {
                $new_flags = $new_flags . $re;
            }
      }

      if (join('', sort split('', $new_flags)) ne join('', sort split('', $flags))) {
          rename "$file", ("$path/cur/$message$sep_char" . "2,$new_flags")
              and print "Sync: changed mail in $mb: $flags -> $new_flags\n";
      }
    }
  }
}
